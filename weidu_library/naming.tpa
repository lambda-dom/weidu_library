//Libraries.
INCLUDE "%WEIDU_LIBRARY_DIR%/2da.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/arrays.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/encoders.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/lists.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/tables.tpa"


//Basic functions.
DEFINE_DIMORPHIC_FUNCTION get_master_namespaces RET path BEGIN
    OUTER_TEXT_SPRINT path "weidu_external/data/weidu_library/master_namespaces.2da"
END

DEFINE_DIMORPHIC_FUNCTION get_extended_namespaces RET path BEGIN
    OUTER_TEXT_SPRINT path "weidu_external/data/weidu_library/extended_namespaces.2da"
END

DEFINE_DIMORPHIC_FUNCTION initialize_extended_namespaces BEGIN
    LAF get_master_namespaces RET destination = path END
    ACTION_IF NOT (FILE_EXISTS "%destination%") BEGIN
        OUTER_TEXT_SPRINT source "%WEIDU_LIBRARY_DIR%/resources/2da/naming/master_namespaces.2da"
        COPY "%source%" "%destination%"
    END

    LAF get_extended_namespaces RET destination = path END
    ACTION_IF NOT (FILE_EXISTS "%destination%") BEGIN
        OUTER_TEXT_SPRINT source "%WEIDU_LIBRARY_DIR%/resources/2da/naming/extended_namespaces.2da"
        COPY "%source%" "%destination%"
    END
END


//Initialize: guarantee the tables exist.
LAF initialize_extended_namespaces END


//Constants.
LAF get_master_namespaces RET path_master_namespaces = path END

//Order: load table and indexes *after* initialization.
//Tables.
LAF load_table STR_VAR
    file = "%master_namespaces_path%"
    fields = "namespace,abbrev,index"
RET_ARRAY master_namespaces = table END

//Indexes.
LAF array_from_table STR_VAR
    table = "master_namespaces"
    key = "namespace"
    value = "abbrev"
RET_ARRAY namespace_abbrevs = array END

LAF array_from_table STR_VAR
    table = "master_namespaces"
    key = "namespace"
    value = "index"
RET_ARRAY namespace_indices = array END


//Loaders.
DEFINE_DIMORPHIC_FUNCTION load_master_namespaces RET_ARRAY namespaces BEGIN
    LAF get_master_namespaces RET path END
    LAF load_table STR_VAR
        file = "%path%"
        fields = "namespace,abbrev,index"
    RET_ARRAY namespaces = table END
END

DEFINE_DIMORPHIC_FUNCTION load_namespaces RET_ARRAY namespaces BEGIN
    LAF get_extended_namespaces RET path END
    LAF load_table STR_VAR
        file = "%path%"
        fields = "symbol,namespace,level,index,resource"
    RET_ARRAY namespaces = table END
END


//Patching functions.
DEFINE_PATCH_FUNCTION add_extended_namespace STR_VAR namespace = "" abbreviation = "" BEGIN
    COUNT_2DA_COLS cols
    PATCH_IF NOT (3 = cols) BEGIN
        PATCH_FAIL "add_extended_namespace: expected 3 columns in master table, got '%cols%'."
    END

    //Sanitize abbreviation.
    PATCH_IF NOT (2 = STRING_LENGTH "%abbreviation%") BEGIN
        PATCH_FAIL "add_extended_namespace: abbreviation '%abbreviation%' is not a 2-letter word."
    END ELSE BEGIN
        INNER_PATCH "%abbreviation%" BEGIN
            REPLACE_EVALUATE "[a-z][a-z]" BEGIN
                //Sanitize.
                PATCH_IF NOT ("%abbreviation%" STRING_EQUAL_CASE "%MATCH0%") THEN BEGIN
                    PATCH_FAIL "add_extended_namespace: abbreviation '%abbreviation%' is not a lower-cased 2-letter with no digits, etc."
                END
            END ""
        END
    END

    //Guarantee uniqueness.
    LPF find_row_index INT_VAR col = 1 STR_VAR key = "%abbreviation%" RET row_index = index END
    PATCH_IF NOT (0 - 1 = row_index) BEGIN
        PATCH_FAIL "add_extended_namespace: abbreviation '%abbreviation%' already exists in table."
    END

    LPF find_row_index STR_VAR key = "%symbol%" RET row_index = index END
    PATCH_IF NOT (0 - 1 = row_index) BEGIN
        PATCH_FAIL "add_extended_namespace: namespace '%namespace%' already exists in table."
    END

    COUNT_2DA_ROWS cols rows
    INSERT_2DA_ROW rows cols "%namespace% %abbreviation% %rows%"
END

DEFINE_PATCH_FUNCTION add_extended_spell_symbol INT_VAR
    level = 0
STR_VAR
    symbol = ""
    namespace = ""
    patch = "*"
BEGIN
    //Sanitize.
    PATCH_IF NOT ((0 <= level) AND (level <= 9)) BEGIN
        PATCH_FAIL "add_extended_spell_symbol: level '%level%' is not in the range [0,9]."
    END

    COUNT_2DA_COLS cols
    PATCH_IF NOT (5 = cols) BEGIN
        PATCH_FAIL "add_extended_spell_symbol: expected 5 columns in namespaces table, got '%cols%'."
    END

    //Sanitize class.
    TEXT_SPRINT namespace_id $"namespace_indices"("%namespace%")
    PATCH_IF NOT (IS_AN_INT class_id) BEGIN
        PATCH_FAIL "add_extended_spell_symbol: invalid namespace '%namespace%'."
    END

    //Sanitize symbol.
    LPF find_row_index STR_VAR key = "%symbol%" RET row_index = index END
    PATCH_IF NOT (0 - 1 = row_index) BEGIN
        PATCH_FAIL "add_extended_spell_symbol: symbol '%symbol%' already exists in table."
    END

    //Find last existing index.
    last_index = 0
    COUNT_2DA_ROWS cols rows
    FOR (i = 0; i < rows; ++i) BEGIN
        READ_2DA_ENTRY i 1 cols spl_namespace
        READ_2DA_ENTRY i 2 cols spl_level
        PATCH_IF ("%spl_namespace%" STRING_EQUAL_CASE "%namespace%") AND (spl_level = level) BEGIN
            READ_2DA_ENTRY i 3 cols spl_index
            PATCH_IF (last_index < spl_index) BEGIN
                last_index = spl_index
            END
        END
    END

    //Find first free slot.
    TEXT_SPRINT abbrev $"namespace_abbrevs"("%namespace%")
    TEXT_SPRINT resource ""
    found = 0 - 1
    FOR (i = last_index; (i <= 0xff) AND (found < 0); ++i) BEGIN
        LPF encode_hexadecimal STR_VAR value = "%i%" RET index = return END

        //Strip 0x and normalize to two characters.
        len = STRING_LENGTH "%index%"
        PATCH_IF len > 2 BEGIN
            SNPRINT (0 - len + 2) index "%index%"
        END
        PATCH_IF (1 = STRING_LENGTH "%index%") BEGIN
            TEXT_SPRINT index "0%index%"
        END

        TEXT_SPRINT resource "sp%abbrev%%level%%index%"
        PATCH_IF NOT (FILE_EXISTS_IN_GAME "%resource%.spl") BEGIN
            found = i
        END
    END

    PATCH_IF (found = 0 - 1) BEGIN
        PATCH_FAIL "add_extended_spell_symbol: no free slot in namespace '%namespace%' and level '%level%' was found."
    END

    //Copy blank spell and insert row.
    PATCH_WITH_SCOPE BEGIN
        TEXT_SPRINT blank "%WEIDU_LIBRARY_DIR%/resources/spl/blank.spl"
        PATCH_IF NOT (FILE_EXISTS "%blank%") BEGIN
            PATCH_FAIL "add_extended_spell_symbol: blank spell '%blank%' not found. "
        END

        INNER_ACTION BEGIN
            COPY "%blank%" "override/%resource%.spl"
                PATCH_IF NOT ("%patch%" STRING_EQUAL_CASE "*") BEGIN
                    LPF "%patch%" STR_VAR destination = "%resource%" END
                END
        END
    END

    INSERT_2DA_ROW rows cols "%symbol% %namespace% %level% %found% %resource%"
END


//Searchers.
DEFINE_DIMORPHIC_FUNCTION get_namespace_spell_resource STR_VAR namespaces = "" symbol = "" RET resource BEGIN
    LAF find_table_row STR_VAR
        table = "%namespaces%"
        field = "symbol"
        key = "%symbol%"
    RET row = index END

    OUTER_TEXT_SPRINT resource "*"
    ACTION_IF NOT (0 - 1 = row) BEGIN
        OUTER_TEXT_SPRINT resource $"%namespaces%"("%row%" "symbol")
    END
END
