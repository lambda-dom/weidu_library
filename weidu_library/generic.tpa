//Libraries.
INCLUDE "%WEIDU_LIBRARY_DIR%/binary.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/tables.tpa"


//Builders.
DEFINE_DIMORPHIC_FUNCTION load_offsets_table STR_VAR file = "" RET_ARRAY table BEGIN
    LAF load_table STR_VAR
        file = "%file%"
        fields = "field,offset,reader,writer"
    RET_ARRAY table END
END


//Generic reader and writer.
DEFINE_PATCH_FUNCTION get_field_at INT_VAR offset = 0 STR_VAR field = "" offsets = "" readers = "" RET value BEGIN
    fx_offset = $"%offsets%"("%field%")

    //Sanitize.
    PATCH_IF NOT (IS_AN_INT fx_offset) BEGIN
        PATCH_FAIL "get_field_at: field '%field%' is not a valid field of array '%offsets%'."
    END

    TEXT_SPRINT reader $"%readers%"("%field%")
    LPF "%reader%" INT_VAR offset = (offset + fx_offset) RET value END
END

DEFINE_PATCH_FUNCTION set_field_at INT_VAR offset = 0 STR_VAR field = "" value = "" offsets = "" writers = "" BEGIN
    fx_offset = $"%offsets%"("%field%")

    //Sanitize.
    PATCH_IF NOT (IS_AN_INT fx_offset) BEGIN
        PATCH_FAIL "set_field_at: field '%field%' is not a valid field of array '%offsets%'."
    END

    TEXT_SPRINT writer $"%writers%"("%field%")
    LPF "%writer%" INT_VAR offset = (offset + fx_offset) STR_VAR value = "%value%" END
END
