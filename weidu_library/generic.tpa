//Libraries.
INCLUDE "%WEIDU_LIBRARY_DIR%/tables.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/internal/binary.tpa"


//Builders.
DEFINE_DIMORPHIC_FUNCTION load_offsets_table STR_VAR file = "" RET_ARRAY table BEGIN
    LAF load_table STR_VAR
        file = "%file%"
        fields = "field:offset:reader:writer"
    RET_ARRAY table END
END

//Generic reader and writer.
DEFINE_PATCH_FUNCTION get_field_at INT_VAR offset = 0 STR_VAR field = "" offsets = "" RET value BEGIN
    TEXT_SPRINT key "%field%"
    LPF find_table_row STR_VAR
        table = "%offsets%"
        field = "field"
        key = "%key%"
    RET index END

    //Sanitize.
    PATCH_IF (0 - 1 = index) BEGIN
        PATCH_FAIL "get_field_at: field '%field%' is not a valid field of table '%offsets%'."
    END

    field_offset = $"%offsets%"("%index%" "offset")
    TEXT_SPRINT reader $"%offsets%"("%index%" "reader")
    LPF "%reader%" INT_VAR offset = (offset + field_offset) RET value END
END

DEFINE_PATCH_FUNCTION set_field_at INT_VAR offset = 0 STR_VAR field = "" value = "" offsets = "" BEGIN
    TEXT_SPRINT key "%field%"
    LPF find_table_row STR_VAR
        table = "%offsets%"
        field = "field"
        key = "%key%"
    RET index END

    //Sanitize.
    PATCH_IF (0 - 1 = index) BEGIN
        PATCH_FAIL "set_field_at: field '%field%' is not a valid field of table '%offsets%'."
    END

    field_offset = $"%offsets%"("%index%" "offset")
    TEXT_SPRINT writer $"%offsets%"("%index%" "writer")
    LPF "%writer%" INT_VAR offset = (offset + field_offset) STR_VAR value = "%value%" END
END
