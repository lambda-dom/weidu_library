//Libraries.
INCLUDE "%WEIDU_LIBRARY_DIR%/components.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/projectiles.tpa"


DEFINE_ACTION_FUNCTION copy_add_projectile STR_VAR
    resource = ""
    dir = ""
    destination = ""
    ext = "*"
    patch = "*"
    fields = ""
BEGIN
    //Source.
    OUTER_TEXT_SPRINT source "%dir%/%resource%.pro"
    ACTION_IF NOT (FILE_EXISTS "%source%") BEGIN
        FAIL "copy_add_projectile: source '%source%' of resource '%resource%' does not exist."
    END

    //Extract arguments.
    OUTER_TEXT_SPRINT label $"%fields%"("label")
    OUTER_TEXT_SPRINT clearair $"%fields%"("clearair")

    //Sanitize.
    ACTION_IF NOT (IS_AN_INT clearair) BEGIN
        FAIL "copy_add_projectile: '%clearair%' of projectile '%resource%' is not an integer."
        ACTION_IF NOT ((0 = clearair) OR (1 = clearair)) BEGIN
            FAIL "copy_add_projectile: '%clearair%' of projectile '%resource%' is not an integer."
        END
    END

    //Copy-rename projectile for add_projectile.
    OUTER_TEXT_SPRINT temp_dir "weidu_external/%TP2_BASE_NAME%/%COMPONENT%/resources/pro"
    COPY "%source%" "%temp_dir%/%destination%.pro"
        PATCH_IF NOT ("%patch%" STRING_EQUAL_CASE "*") BEGIN
            LPF "%patch%" STR_VAR
                destination = "%destination%"
            END
        END

    //Add projectile.
    LAF add_projectile STR_VAR
        projectile = "%destination%"
        dir = "%temp_dir%"
        label = "%label%"
    RET _ = id END

    //Append to clearair table.
    //TODO: move to post-processor.
    ACTION_IF NOT (0 = clearair) BEGIN
        //Check label provided.
        ACTION_IF ("%label%" STRING_EQUAL_CASE "*")  BEGIN
            FAIL "copy_add_projectile: invalid label '%label%' for projectile '%resource%'."
        END

        COPY_EXISTING "clearair.2da" "override"
            LPF append_clearair STR_VAR
                projectile = "%destination%"
                label = "%label%"
            END
        PRETTY_PRINT_2DA
        BUT_ONLY
    END
END
