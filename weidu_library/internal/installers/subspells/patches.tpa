//Libraries.
INCLUDE "%WEIDU_LIBRARY_DIR%/arrays.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/ids.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/opcodes.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/spells.tpa"


//Subspell patches.
DEFINE_PATCH_FUNCTION patch_damage STR_VAR arg1 = "" arg2 = "" arg3 = "" BEGIN
    //Sanitize arguments.
    PATCH_IF NOT (IS_AN_INT arg1) BEGIN
        PATCH_FAIL "patch_damage: amount '%arg1%' is not an integer."
    END

    PATCH_IF NOT (IS_AN_INT arg2) BEGIN
        PATCH_FAIL "patch_damage: dicenumber '%arg2%' is not an integer."
    END

    PATCH_IF NOT (IS_AN_INT arg3) BEGIN
        PATCH_FAIL "patch_damage: dicesize '%arg3%' is not an integer."
    END

    PATCH_IF (arg1 < 0) OR (arg2 < 0) OR (arg3 < 0) BEGIN
        PATCH_FAIL "patch_damage: all damage parameters must be positive."
    END

    //Implicit number conversion.
    amount = arg1
    dicenumber = arg2
    dicesize = arg3

    //Damage.
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 12                   //Damage.
        parameter1 = amount
        dicenumber = dicenumber
        dicesize = dicesize
    END
END

DEFINE_PATCH_FUNCTION patch_duration STR_VAR arg1 = "" arg2 = "*" arg3 = "*" BEGIN
    //Sanitize arguments.
    PATCH_IF NOT (IS_AN_INT arg1) BEGIN
        PATCH_FAIL "patch_duration: arg1 '%arg1%' is not an integer."
    END

    PATCH_IF (arg1 <= 0) BEGIN
        PATCH_FAIL "patch_duration: duration '%arg1%' must be strictly positive."
    END
 
    //Implicit number conversion.
    duration = arg1
    LPF ALTER_SPELL_EFFECT INT_VAR
        duration_high = duration * 6
    END
END


//Specific patchers.
DEFINE_PATCH_FUNCTION hold_creature STR_VAR arg1 = "" arg2 = "" arg3 = "" BEGIN
    //Sanitize arguments.
    LPF get_ids_file_id STR_VAR file = "%arg2%" RET ids_id = id END
    PATCH_IF (0 - 1 = ids_id) BEGIN
        PATCH_FAIL "hold_creature: resource '%arg2%' not a valid ids file."
    END

    LPF get_id_from_ids STR_VAR ids = "%arg2%" value = "%arg3%" RET id = id END
    PATCH_IF (0 - 1 = id) BEGIN
        PATCH_FAIL "hold_creature: id '%arg3%' not present in ids file '%arg2%.ids'."
    END

    //Patch hold and Use Eff's.
    LPF ALTER_EFFECT INT_VAR
        check_globals = 0
        match_opcode = 175                  //Hold creature.
        parameter1 = ids_id
        parameter2 = id
    END

    LPF ALTER_EFFECT INT_VAR
        check_globals = 0
        match_opcode = 177                  //Use Eff.
        parameter1 = ids_id
        parameter2 = id
    END

    //Patch duration.
    LPF patch_duration STR_VAR arg1 = "%arg1%" END
END

DEFINE_PATCH_FUNCTION charm STR_VAR arg1 = "" arg2 = "" arg3 = "" BEGIN
    //Sanitize arguments.
    PATCH_IF NOT (IS_AN_INT arg3) BEGIN
        PATCH_FAIL "charm: mode '%arg3%' is not an integer."
    END

    PATCH_IF NOT (((0 <= arg3) AND (arg3 <= 5)) OR ((1000 <= arg3) AND (arg3 <= 1005))) BEGIN
        PATCH_FAIL "charm: mode '%arg3%' must be in the [0,5] or [1000, 1005] range."
    END

    LPF get_id_from_ids STR_VAR ids = "general" value = "%arg2%" RET type = id END
    PATCH_IF (0 - 1 = type) BEGIN
        PATCH_FAIL "charm: type '%arg2%' is not a member of 'general.ids'."
    END

    LPF ALTER_EFFECT INT_VAR
        check_globals = 0
        match_opcode = 5
        parameter1 = type
        parameter2 = arg3
    END

    //Patch duration.
    LPF patch_duration STR_VAR arg1 = "%arg1%" END
END

DEFINE_PATCH_FUNCTION poisoning STR_VAR arg1 = "" arg2 = "" arg3 = "*" BEGIN
    //Sanitize arguments.
    PATCH_IF NOT (IS_AN_INT arg2) BEGIN
        PATCH_FAIL "poisoning: amount '%arg2%' is not an integer."
    END

    PATCH_IF (arg2 <= 0) BEGIN
        PATCH_FAIL "poisoning: amount '%amount%' must be strictly positive."
    END

    //Patch amount.
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 25                   //Poison.
        parameter1 = arg2
    END

    //Patch duration.
    LPF patch_duration STR_VAR arg1 = "%arg1%" END
END

DEFINE_PATCH_FUNCTION splash_damage STR_VAR arg1 = "" arg2 = "" arg3 = "*" BEGIN
    //Sanitize arguments.
    PATCH_IF NOT (IS_AN_INT arg1) BEGIN
        PATCH_FAIL "splash_damage: duration '%arg1%' is not an integer."
    END

    PATCH_IF NOT (IS_AN_INT arg2) BEGIN
        PATCH_FAIL "splash_damage: amount '%arg2%' is not an integer."
    END

    PATCH_IF (arg2 <= 0) BEGIN
        PATCH_FAIL "splash_damage: amount '%amount%' must be strictly positive."
    END

    //Number conversions.
    duration = arg1
    amount = arg2

    //Patch amount.
    LPF ALTER_EFFECT INT_VAR
        match_opcode = 12                   //Damage.
        parameter1 = amount
    END

    //Patch duration.
    LPF patch_duration STR_VAR arg1 = "%duration%" END

    //Add remaining delayed damage opcodes.
    PATCH_IF (duration >= 2) BEGIN
        FOR (i = 1; i < duration; ++i) BEGIN
            LPF CLONE_EFFECT INT_VAR
                match_opcode = 12           //Damage.
                match_timing = 4            //Delayed.
                //(duration - i) to get ordered opcodes.
                duration = 3 + (duration - i) * 6
            STR_VAR
                insert = "below"
            END
        END
    END
END

DEFINE_PATCH_FUNCTION slow_movement_rate STR_VAR arg1 = "" arg2 = "" arg3 = "*" BEGIN
    //Sanitize arguments.
    PATCH_IF NOT (IS_AN_INT arg2) BEGIN
        PATCH_FAIL "slow_movement_rate: amount '%arg2%' is not an integer."
    END

    PATCH_IF (arg2 <= 0) BEGIN
        PATCH_FAIL "slow_movement_rate: amount '%arg2%' must be strictly positive."
    END

    //Patch amount.
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 126                  //Movement rate.
        parameter1 = 0 - arg2
    END

    //Patch duration.
    LPF patch_duration STR_VAR arg1 = "%arg1%" END
END

DEFINE_PATCH_FUNCTION cripple STR_VAR arg1 = "" arg2 = "" arg3 = "*" BEGIN
    //Sanitize arguments.
    PATCH_IF NOT (IS_AN_INT arg2) BEGIN
        PATCH_FAIL "cripple: amount '%arg2%' is not an integer."
    END

    PATCH_IF NOT ((1 <= arg2) AND (arg2 <= 100)) BEGIN
        PATCH_FAIL "cripple: percentage '%amount%' must be in the range [1, 100]."
    END

    //Number conversions.
    amount = arg2
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 126                  //Movement rate.
        parameter1 = amount
    END

    //Patch duration.
    LPF patch_duration STR_VAR arg1 = "%arg1%" END
END

DEFINE_PATCH_FUNCTION level_drain STR_VAR arg1 = "" arg2 = "" arg3 = "*" BEGIN
    //Sanitize arguments.
    PATCH_IF NOT (IS_AN_INT arg2) BEGIN
        PATCH_FAIL "level_drain: amount '%arg2%' is not an integer."
    END

    PATCH_IF (arg2 <= 0) BEGIN
        PATCH_FAIL "level_drain: amount '%arg2%' must be strictly positive."
    END
   
    //Number conversions.
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 216                  //Level drain.
        parameter1 = arg2
    END

    //Patch duration.
    LPF patch_duration STR_VAR arg1 = "%arg1%" END
END

DEFINE_PATCH_FUNCTION drain_strength STR_VAR arg1 = "" arg2 = "" arg3 = "*" BEGIN
    //Sanitize arguments.
    PATCH_IF NOT (IS_AN_INT arg2) BEGIN
        PATCH_FAIL "drain_strength: amount '%arg2%' is not an integer."
    END

    PATCH_IF (arg2 <= 0) BEGIN
        PATCH_FAIL "drain_strength: amount to drain '%amount%' must be strictly positive."
    END
   
    //Number conversions.
    amount = arg2
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 44                   //Strength penalty.
        parameter1 = 0 - amount
    END

    //Patch duration.
    LPF patch_duration STR_VAR arg1 = "%arg1%" END
END

DEFINE_PATCH_FUNCTION knockback STR_VAR arg1 = "" arg2 = "" arg3 = "*" BEGIN
    //Sanitize arguments.
    PATCH_IF NOT (IS_AN_INT arg2) BEGIN
        PATCH_FAIL "knockback: amount '%arg2%' is not an integer."
    END

    PATCH_IF (arg2 <= 0) BEGIN
        PATCH_FAIL "knockback: amount '%arg2%' must be strictly positive."
    END
   
    //Number conversions.
    amount = arg2
    LPF ALTER_SPELL_EFFECT INT_VAR
        match_opcode = 235                  //Wing buffet.
        parameter1 = amount
    END

    //Patch duration.
    LPF patch_duration STR_VAR arg1 = "%arg1%" END
END
