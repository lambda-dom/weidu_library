//Libraries.
INCLUDE "%WEIDU_LIBRARY_DIR%/arrays.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/spells.tpa"


//Arrays.
LAF load_array STR_VAR
    table = "%WEIDU_LIBRARY_DIR%/resources/2da/spells/add_types.2da"
RET_ARRAY add_types = array END


DEFINE_ACTION_FUNCTION add_symbol STR_VAR
    resource = ""
    dir = "*"
    destination = "*"
    ext = "*"
    patch = "*"
    fields = ""
BEGIN
    //Resource existence check.
    OUTER_TEXT_SPRINT blank "%WEIDU_LIBRARY_DIR%/resources/spl/blank.spl"
    ACTION_IF NOT (FILE_EXISTS "%blank%") BEGIN
        FAIL "add_symbol: blank spell '%blank%' not found."
    END

    //Extract arguments.
    OUTER_TEXT_SPRINT type $"%fields%"("type")
    OUTER_TEXT_SPRINT level $"%fields%"("level")

    //Sanitize.
    ACTION_IF NOT (IS_AN_INT level) BEGIN
        FAIL "write_symbol: '%level%' of symbol '%resource%' is not an integer."
    END ELSE BEGIN
        ACTION_IF NOT ((0 <= level) AND (level <= 10)) BEGIN
            FAIL "add_symbol: '%level%' of symbol '%resource%' must be in the [0-10] range."
        END
    END

    LAF get_array_element STR_VAR
        array = "add_types"
        key = "%type%"
    RET id = value END
    ACTION_IF (0 - 1 = id) BEGIN
        FAIL "add_symbol: '%type%' of symbol '%resource%' is not a valid spell type."
    END

    LAF get_spell_res STR_VAR spell = "%resource%" RET exists = resource END
    ACTION_IF NOT ("%exists%" STRING_EQUAL_CASE "*") BEGIN
        FAIL "add_symbol: symbol '%resource%' already exists in 'spell.ids'."
    END

    //Add symbol.
    ADD_SPELL "%blank%" id level "%resource%"
        PATCH_IF NOT ("%patch%" STRING_EQUAL_CASE "*") BEGIN
            LPF "%patch%" STR_VAR
                destination = "%destination%"
            END
        END
END
