//Libraries.
INCLUDE "%WEIDU_LIBRARY_DIR%/creatures.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/installers.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/spells.tpa"


//Spell patchers.
DEFINE_PATCH_FUNCTION add_display_string INT_VAR tra_ref = 0 insert_point = 0 - 1 BEGIN
    tlk_ref = RESOLVE_STR_REF ((AT tra_ref))
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 139                        //Display string.
        parameter1 = tlk_ref
        timing = 1                          //Instantaneous.
        target = 2                          //Other.
        insert_point = insert_point
    END
END

DEFINE_PATCH_FUNCTION patch_resource_reference INT_VAR
    header = 0 - 1
    match_opcode = 0
STR_VAR
    match_resource = ""
    resource = ""
    ext = "spl"
    references = ""
BEGIN
    LPF get_table_ref STR_VAR
        value = "%resource%"
        array = "%references%"
        ext = "%ext%"
    RET res = resource END

    LPF ALTER_EFFECT INT_VAR
        verbose = 1
        header = header
        match_opcode = match_opcode
    STR_VAR
        match_resource = "%match_resource%"
        resource = "%res%"
    END
END

//Creature patchers.
DEFINE_PATCH_FUNCTION creature_add_item STR_VAR
    reference = ""
    slot = ""
    flags = "identified&unstealable&undroppable"
    array = ""
BEGIN
    LPF get_table_ref STR_VAR
        value = "%reference%"
        array = "%array%"
        ext = "itm"
    RET item = resource END

    //Sanitize slot.
    LPF get_creature_slot_id STR_VAR slot = "%slot%" RET id = id END
    PATCH_IF (0 - 1 = id) BEGIN
        PATCH_FAIL "creature_add_item: invalid creature slot '%slot%'."
    END

    //Equip item.
    ADD_CRE_ITEM "%item%" #0 #0 #0 "%flags%" "%slot%"
END


//Script patchers.
DEFINE_PATCH_FUNCTION patch_script_spell_replacements STR_VAR replacements = "" BEGIN
    PHP_EACH "%replacement%" AS string => spell BEGIN
        //Sanitize.
        PATCH_IF ( 7 < STRING_LENGTH "%spell%") BEGIN
            PATCH_FAIL "patch_script_spell_replacements: resource '%spell%' has more than 7 characters."
        END

        PATCH_IF NOT (FILE_EXISTS_IN_GAME "%spell%.spl") BEGIN
            PATCH_FAIL "patch_script_spell_replacements: spell '%spell%.spl' does not exist in game."
        END

        TO_UPPER spell

        //Perform substitution.
        REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH "%string%" "%spell%"
    END
END

DEFINE_PATCH_FUNCTION patch_script_item_replacements STR_VAR replacements = "" BEGIN
    PHP_EACH "%replacement%" AS string => item BEGIN
        //Sanitize.
        PATCH_IF NOT (FILE_EXISTS_IN_GAME "%item%.itm") BEGIN
            PATCH_FAIL "patch_script_item_replacements: item '%item%.itm' does not exist in game."
        END

        //Perform substitution.
        REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH "%string%" "%item%"
    END
END

DEFINE_PATCH_FUNCTION patch_script_string_replacements STR_VAR replacements = "" tra = "" BEGIN
    PATCH_IF NOT (FILE_EXISTS "%tra%.tra") BEGIN
        PATCH_FAIL "patch_script_string_replacements: tra file '%tra%.tra' does not exist."
    END

    PATCH_WITH_TRA "%tra%.tra" BEGIN
        PHP_EACH "%replacement%" AS string => tra_ref BEGIN
            tlk_ref = RESOLVE_STR_REF ((AT tra_ref))

            //Perform substitution.
            REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH "%string%" "%tlk_ref%"
        END
    END
END
