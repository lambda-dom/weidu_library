//Libraries.
INCLUDE "%WEIDU_LIBRARY_DIR%/creatures.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/installers.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/spells.tpa"


//Spell patchers.
DEFINE_PATCH_FUNCTION add_display_string INT_VAR tra_ref = 0 insert_point = 0 - 1 BEGIN
    tlk_ref = RESOLVE_STR_REF ((AT tra_ref))
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 139                        //Display string.
        parameter1 = tlk_ref
        timing = 1                          //Instantaneous.
        target = 2                          //Other.
        insert_point = insert_point
    END
END

DEFINE_PATCH_FUNCTION clone_to_display_string INT_VAR
    check_headers = 1
    check_globals = 0
    header = 0 - 1
    match_opcode = 142
    tra_ref = 0
STR_VAR
    insert = "below"
BEGIN
    //Insert text.
    ref = RESOLVE_STR_REF ((AT tra_ref))
    LPF CLONE_EFFECT INT_VAR
        check_globals = check_globals
        check_headers = check_headers
        header = header
        verbose = 1
        match_opcode = match_opcode
        opcode = 139                        //Display string.
        parameter1 = ref
        parameter2 = 0                      //Clean.
        timing = 1                          //Instantaneous.
        duration = 0
        special = 0                         //Clean.
    STR_VAR
        resource = ""                       //Clean.
        insert = "%insert%"
    END
END

DEFINE_ACTION_FUNCTION patch_spell_duration INT_VAR turns = 2 STR_VAR spell = "" BEGIN
    LAF get_spell_res STR_VAR
        spell = "%spell%"
    RET res = resource END

    ACTION_IF ("%res%" STRING_EQUAL_CASE "*") BEGIN
        FAIL "patch_spell_duration: spell '%spell%' not found in 'spell.ids'"
    END

    COPY_EXISTING "%res%.spl" "override"
        //Alter duration.
        LPF ALTER_SPELL_EFFECT INT_VAR
            check_globals = 0
            duration = turns * 60
        END

        //Patch description.
        LPF get_spell_field STR_VAR field = "description" RET tlk = value END
        GET_STRREF tlk description
        INNER_PATCH_SAVE new "%description%" BEGIN
            REPLACE_TEXTUALLY "^Duration: [1-9][0-9]* \(turn\|round\)\(s\)?" "Duration: %turns% turns"
        END
        ref = RESOLVE_STR_REF ("%new%")
        LPF set_spell_field STR_VAR field = "description" value = "%ref%" END
    BUT_ONLY
END

//Creature patchers.
DEFINE_PATCH_FUNCTION creature_add_item STR_VAR
    reference = ""
    slot = ""
    flags = "identified&unstealable&undroppable"
    array = ""
BEGIN
    LPF get_table_ref STR_VAR
        value = "%reference%"
        array = "%array%"
        ext = "itm"
    RET item = resource END

    //Sanitize slot.
    LPF get_creature_slot_id STR_VAR slot = "%slot%" RET id = id END
    PATCH_IF (0 - 1 = id) BEGIN
        PATCH_FAIL "creature_add_item: invalid creature slot '%slot%'."
    END

    //Equip item.
    ADD_CRE_ITEM "%item%" #0 #0 #0 "%flags%" "%slot%"
END
