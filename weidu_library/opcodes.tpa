//Libraries.
INCLUDE "%WEIDU_LIBRARY_DIR%/arrays.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/generic.tpa"


//Basic functions.
DEFINE_DIMORPHIC_FUNCTION get_opcode_size RET size BEGIN
    OUTER_SET size = 0x30
END


//Constants.
//Tables.
LAF load_offsets_table STR_VAR
    file = "%WEIDU_LIBRARY_DIR%/resources/2da/opcodes/offsets.2da"
RET_ARRAY opcode_offsets = table END

//Arrays.
LAF load_array STR_VAR
    table = "%WEIDU_LIBRARY_DIR%/resources/2da/opcodes/types.2da"
RET_ARRAY opcode_types = array END

LAF load_array STR_VAR
    table = "%WEIDU_LIBRARY_DIR%/resources/2da/opcodes/targets.2da"
RET_ARRAY opcode_targets = array END

LAF load_array STR_VAR
    table = "%WEIDU_LIBRARY_DIR%/resources/2da/opcodes/timings.2da"
RET_ARRAY opcode_timings = array END

LAF load_array STR_VAR
    table = "%WEIDU_LIBRARY_DIR%/resources/2da/opcodes/resist_dispels.2da"
RET_ARRAY opcode_resist_dispels = array END

LAF load_array STR_VAR
    table = "%WEIDU_LIBRARY_DIR%/resources/2da/opcodes/save_flags.2da"
RET_ARRAY save_flags = array END

LAF load_array STR_VAR
    table = "%WEIDU_LIBRARY_DIR%/resources/2da/opcodes/damage_modes.2da"
RET_ARRAY damage_modes = array END

LAF load_array STR_VAR
    table = "%WEIDU_LIBRARY_DIR%/resources/2da/opcodes/damage_types.2da"
RET_ARRAY damage_types = array END

LAF load_array STR_VAR
    table = "%WEIDU_LIBRARY_DIR%/resources/2da/opcodes/ids.2da"
RET_ARRAY file_ids = array END


//Getters.
DEFINE_DIMORPHIC_FUNCTION get_opcode_type_id STR_VAR value = "" RET id BEGIN
    //Lower-case to avoid trivial mismatches.
    ACTION_TO_LOWER value
    OUTER_TEXT_SPRINT type $"opcode_types"("%value%")

    OUTER_SET id = 0 - 1
    ACTION_IF (IS_AN_INT type) BEGIN
        OUTER_SET id = type
    END
END

DEFINE_DIMORPHIC_FUNCTION get_opcode_target_id STR_VAR value = "" RET id BEGIN
    //Lower-case to avoid trivial mismatches.
    ACTION_TO_LOWER value
    OUTER_TEXT_SPRINT target $"opcode_targets"("%value%")

    OUTER_SET id = 0 - 1
    ACTION_IF (IS_AN_INT target) BEGIN
        OUTER_SET id = target
    END
END

DEFINE_DIMORPHIC_FUNCTION get_opcode_timing_id STR_VAR value = "" RET id BEGIN
    //Lower-case to avoid trivial mismatches.
    ACTION_TO_LOWER value
    OUTER_TEXT_SPRINT timing $"opcode_timings"("%value%")

    OUTER_SET id = 0 - 1
    ACTION_IF IS_AN_INT timing BEGIN
        OUTER_SET id = timing
    END
END

DEFINE_DIMORPHIC_FUNCTION get_opcode_resist_dispel_id STR_VAR value = "" RET id BEGIN
    //Lower-case to avoid trivial mismatches.
    ACTION_TO_LOWER value
    OUTER_TEXT_SPRINT resist_dispel $"opcode_resist_dispels"("%value%")

    OUTER_SET id = 0 - 1
    ACTION_IF IS_AN_INT resist_dispel BEGIN
        OUTER_SET id = resist_dispel
    END
END

DEFINE_DIMORPHIC_FUNCTION get_save_flag_index STR_VAR value = "" RET index BEGIN
    //Lower-case to avoid trivial mismatches.
    ACTION_TO_LOWER value
    OUTER_TEXT_SPRINT flag $"save_flags"("%value%")

    OUTER_SET index = 0 - 1
    ACTION_IF IS_AN_INT flag BEGIN
        OUTER_SET index = flag
    END
END

DEFINE_DIMORPHIC_FUNCTION get_ids_file_id STR_VAR file = "" RET id BEGIN
    //Lower-case to avoid trivial mismatches.
    ACTION_TO_LOWER file
    OUTER_TEXT_SPRINT file_id $"file_ids"("%file%")

    OUTER_SET id = 0 - 1
    ACTION_IF IS_AN_INT file_id BEGIN
        OUTER_SET id = file_id
    END
END


//Readers and writers.
DEFINE_PATCH_FUNCTION get_opcode_field INT_VAR offset = 0 STR_VAR field = "" RET value BEGIN
    LPF get_field_at INT_VAR
        offset = offset
    STR_VAR
        field = "%field%"
        offsets = "opcode_offsets"
    RET value END
END

DEFINE_PATCH_FUNCTION set_opcode_field INT_VAR offset = 0 STR_VAR field = "" value = "" BEGIN
    LPF set_field_at INT_VAR
        offset = offset
    STR_VAR
        field = "%field%"
        offsets = "opcode_offsets"
        value = "%value%"
    END
END


//Opcodes as arrays.
DEFINE_DIMORPHIC_FUNCTION get_zero_opcode RET_ARRAY array BEGIN
    ACTION_DEFINE_ASSOCIATIVE_ARRAY array BEGIN
        opcode          => "ac_damage_type"
        target          => "projectile"
        power           => "0"
        parameter1      => "#num:0"
        parameter2      => "#num:0"
        timing          => "limited"
        resist_dispel   => "natural"
        duration        => "0"
        probability1    => "100"
        probability2    => "0"
        resource        => "#nul:"
        dicenumber      => "0"
        dicesize        => "0"
        savingthrow     => "#num:0"
        savebonus       => "0"
        special         => "#num:0"
    END
END

DEFINE_PATCH_FUNCTION get_opcode_array INT_VAR offset = 0 RET_ARRAY array BEGIN
    DEFINE_ASSOCIATIVE_ARRAY array BEGIN END

    //Opcode type.
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "opcode" RET value END
    LPF find_key STR_VAR
        array = "opcode_types"
        value = "%value%"
    RET opcode_id = key END
    PATCH_IF ("%opcode_id%" STRING_EQUAL_CASE "*") BEGIN
        PATCH_FAIL "get_opcode_array: opcode value '%value%' at offset '%offset%' unknown."
    END
    TEXT_SPRINT $array("opcode") "%opcode_id%"

    //Opcode target.
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "target" RET value END
    LPF find_key STR_VAR
        array = "opcode_targets"
        value = "%value%"
    RET target_id = key END
    PATCH_IF ("%target_id%" STRING_EQUAL_CASE "*") BEGIN
        PATCH_FAIL "get_opcode_array: opcode target '%value%' at offset '%offset%' unknown."
    END
    TEXT_SPRINT $array("target") "%target_id%"

    //Opcode power.
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "power" RET value END
    TEXT_SPRINT $array("power") "%value%"

    //Opcode parameters.
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "parameter1" RET value END
    TEXT_SPRINT $array("parameter1") "#num:%value%"
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "parameter2" RET value END
    TEXT_SPRINT $array("parameter2") "#num:%value%"

    //Opcode timing.
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "timing" RET value END
    LPF find_key STR_VAR
        array = "opcode_timings"
        value = "%value%"
    RET timing_id = key END
    PATCH_IF ("%timing_id%" STRING_EQUAL_CASE "*") BEGIN
        PATCH_FAIL "get_opcode_array: opcode timing '%value%' at offset '%offset%' unknown."
    END
    TEXT_SPRINT $array("timing") "%timing_id%"

    //Opcode resist_dispel.
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "resist_dispel" RET value END
    LPF find_key STR_VAR
        array = "opcode_resist_dispels"
        value = "%value%"
    RET resist_dispel_id = key END
    PATCH_IF ("%resist_dispel_id%" STRING_EQUAL_CASE "*") BEGIN
        PATCH_FAIL "get_opcode_array: opcode resist_dispel '%value%' at offset '%offset%' unknown."
    END
    TEXT_SPRINT $array("resist_dispel") "%resist_dispel_id%"

    //Opcode duration.
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "duration" RET value END
    TEXT_SPRINT $array("duration") "%value%"

    //Opcode probability.
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "probability1" RET value END
    TEXT_SPRINT $array("probability1") "%value%"
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "probability2" RET value END
    TEXT_SPRINT $array("probability2") "%value%"

    //Opcode resource.
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "resource" RET value END
    PATCH_IF ("%value%" STRING_EQUAL_CASE "") BEGIN
        TEXT_SPRINT $array("resource") "#nul:"
    END ELSE BEGIN
        TEXT_SPRINT $array("resource") "#lit:%value%"
    END

    //Opcode dice.
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "dicesize" RET value END
    TEXT_SPRINT $array("dicesize") "%value%"
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "dicenumber" RET value END
    TEXT_SPRINT $array("dicenumber") "%value%"

    //Opcode saves.
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "savingthrow" RET value END
    TEXT_SPRINT $array("savingthrow") "#num:%value%"
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "savebonus" RET value END
    TEXT_SPRINT $array("savebonus") "%value%"

    //Opcode special.
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "special" RET value END
    TEXT_SPRINT $array("special") "#num:%value%"
END
