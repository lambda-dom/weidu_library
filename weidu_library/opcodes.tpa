//Libraries.
INCLUDE "%WEIDU_LIBRARY_DIR%/arrays.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/generic.tpa"


//Basic functions.
DEFINE_DIMORPHIC_FUNCTION get_opcode_size RET size BEGIN
    OUTER_SET size = 0x30
END


//Constants.
//Tables.
LAF load_offsets_table STR_VAR
    file = "%WEIDU_LIBRARY_DIR%/resources/2da/opcodes/offsets.2da"
RET_ARRAY opcode_offsets = table END

//Arrays.
LAF load_array STR_VAR
    table = "%WEIDU_LIBRARY_DIR%/resources/2da/opcodes/types.2da"
RET_ARRAY opcode_types = array END

LAF load_array STR_VAR
    table = "%WEIDU_LIBRARY_DIR%/resources/2da/opcodes/targets.2da"
RET_ARRAY opcode_targets = array END

LAF load_array STR_VAR
    table = "%WEIDU_LIBRARY_DIR%/resources/2da/opcodes/timings.2da"
RET_ARRAY opcode_timings = array END

LAF load_array STR_VAR
    table = "%WEIDU_LIBRARY_DIR%/resources/2da/opcodes/resist_dispels.2da"
RET_ARRAY opcode_resist_dispels = array END

LAF load_array STR_VAR
    table = "%WEIDU_LIBRARY_DIR%/resources/2da/opcodes/save_flags.2da"
RET_ARRAY save_flags = array END

LAF load_array STR_VAR
    table = "%WEIDU_LIBRARY_DIR%/resources/2da/opcodes/damage_modes.2da"
RET_ARRAY damage_modes = array END

LAF load_array STR_VAR
    table = "%WEIDU_LIBRARY_DIR%/resources/2da/opcodes/damage_types.2da"
RET_ARRAY damage_types = array END

LAF load_array STR_VAR
    table = "%WEIDU_LIBRARY_DIR%/resources/2da/opcodes/ids.2da"
RET_ARRAY ids_ids = array END


//Getters.
DEFINE_DIMORPHIC_FUNCTION get_opcode_type_id STR_VAR value = "" RET id BEGIN
    LAF get_array_element STR_VAR
        array = "opcode_types"
        key = "%value%"
    RET type = value END

    OUTER_SET id = 0 - 1
    ACTION_IF NOT ("%type%" STRING_EQUAL_CASE "*") BEGIN
        OUTER_SET id = type
    END
END

DEFINE_DIMORPHIC_FUNCTION get_opcode_target_id STR_VAR value = "" RET id BEGIN
    LAF get_array_element STR_VAR
        array = "opcode_targets"
        key = "%value%"
    RET target = value END

    OUTER_SET id = 0 - 1
    ACTION_IF NOT ("%target%" STRING_EQUAL_CASE "*") BEGIN
        OUTER_SET id = target
    END
END

DEFINE_DIMORPHIC_FUNCTION get_opcode_timing_id STR_VAR value = "" RET id BEGIN
    LAF get_array_element STR_VAR
        array = "opcode_timings"
        key = "%value%"
    RET timing = value END

    OUTER_SET id = 0 - 1
    ACTION_IF NOT ("%timing%" STRING_EQUAL_CASE "*") BEGIN
        OUTER_SET id = timing
    END
END

DEFINE_DIMORPHIC_FUNCTION get_opcode_resist_dispel_id STR_VAR value = "" RET id BEGIN
    LAF get_array_element STR_VAR
        array = "opcode_resist_dispels"
        key = "%value%"
    RET resist_dispel = value END

    OUTER_SET id = 0 - 1
    ACTION_IF NOT ("%resist_dispel%" STRING_EQUAL_CASE "*") BEGIN
        OUTER_SET id = resist_dispel
    END
END

DEFINE_DIMORPHIC_FUNCTION get_save_flag_index STR_VAR value = "" RET index BEGIN
    LAF get_array_element STR_VAR
        array = "save_flags"
        key = "%value%"
    RET flag = value END

    OUTER_SET index = 0 - 1
    ACTION_IF NOT ("%flag%" STRING_EQUAL_CASE "*") BEGIN
        OUTER_SET index = flag
    END
END

DEFINE_DIMORPHIC_FUNCTION get_ids_file_id STR_VAR file = "" RET id BEGIN
    LAF get_array_element STR_VAR
        array = "ids_ids"
        key = "%file%"
    RET ids_id = value END

    OUTER_SET id = 0 - 1
    ACTION_IF NOT ("%ids_id%" STRING_EQUAL_CASE "*") BEGIN
        OUTER_SET id = ids_id
    END
END

//Readers and writers.
DEFINE_PATCH_FUNCTION get_opcode_field INT_VAR offset = 0 STR_VAR field = "" RET value BEGIN
    LPF get_field_at INT_VAR
        offset = offset
    STR_VAR
        field = "%field%"
        offsets = "opcode_offsets"
    RET value END
END

DEFINE_PATCH_FUNCTION set_opcode_field INT_VAR offset = 0 STR_VAR field = "" value = "" BEGIN
    LPF set_field_at INT_VAR
        offset = offset
    STR_VAR
        field = "%field%"
        offsets = "opcode_offsets"
        value = "%value%"
    END
END
