//Libraries.
INCLUDE "%WEIDU_LIBRARY_DIR%/2da.tpa"


//Getters functions.
DEFINE_PATCH_FUNCTION patch_get_sectype_id STR_VAR sectype = "" RET id BEGIN
    //Get sectype.
    LPF find_row_index STR_VAR key = "%sectype%" RET id = index END
    
    //Correct index to account for 2DA spurious line.
    PATCH_IF NOT (0 - 1 = id) BEGIN
        id -= 1
    END
END

DEFINE_DIMORPHIC_FUNCTION get_sectype_id STR_VAR sectype = "" RET id BEGIN
    COPY_EXISTING - "msectype.2da" "override"
        LPF patch_get_sectype_id STR_VAR sectype = "%sectype%" RET id END
END


//Encoders.
DEFINE_DIMORPHIC_FUNCTION encode_spell_sectype STR_VAR value = "" RET return BEGIN
    LAF get_sectype_id STR_VAR sectype = "%value%" RET return = id END

    ACTION_IF (0 - 1 = return) BEGIN
        FAIL "encode_spell_sectype: invalid sectype '%value%'."
    END
END


//Add sectype.
DEFINE_DIMORPHIC_FUNCTION add_sectype INT_VAR tra_ref = 0 STR_VAR sectype = "" RET id BEGIN
    ADD_SECTYPE "%sectype%" (AT tra_ref)
    OUTER_SET id = EVAL "%sectype%"
END
