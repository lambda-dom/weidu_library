//Libraries.
INCLUDE "%WEIDU_LIBRARY_DIR%/2da.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/arrays.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/hlas.tpa"


//Arrays.
//Kit name conversions for enginest.2da gui help strings.
LAF load_array STR_VAR
    table = "%WEIDU_LIBRARY_DIR%/resources/2da/kits/kit_names.2da"
RET_ARRAY kit_names = array END

//Kit name conversions for class descriptions.
LAF load_array STR_VAR
    table = "%WEIDU_LIBRARY_DIR%/resources/2da/kits/kit_classes.2da"
RET_ARRAY kit_classes = array END

LAF load_array STR_VAR
    table = "%WEIDU_LIBRARY_DIR%/resources/2da/kits/kit_spell_progression.2da"
RET_ARRAY kit_spell_progression = array END


//Basic functions.
DEFINE_DIMORPHIC_FUNCTION get_kit_clab STR_VAR kit = "" RET clab BEGIN
    COPY_EXISTING - "kitlist.2da" "override"
        //Find clab file of kit.
        LPF find_column_index STR_VAR key = "abilities" RET col = index END
        PATCH_IF (col = 0 - 1) BEGIN
            PATCH_FAIL "get_kit_clab: column 'abilities' not found in 'kitlist.2da'."
        END

        LPF find_row_index INT_VAR col = 1 STR_VAR key = "%kit%" RET row = index END
        PATCH_IF NOT (row = 0 - 1) BEGIN
            COUNT_2DA_COLS cols
            READ_2DA_ENTRY row col cols clab
        END ELSE BEGIN
            TEXT_SPRINT clab "*"
        END
END

DEFINE_DIMORPHIC_FUNCTION get_kit_spell_progression STR_VAR kit = "" RET table BEGIN
    LAF get_array_element STR_VAR
        array = "kit_spell_progression"
        key = "%kit%"
        default = "*"
    RET table = value END
END


//Auxiliary description functions.
DEFINE_ACTION_FUNCTION set_clastext_description INT_VAR tlk = 0 STR_VAR class = "" BEGIN
    COPY_EXISTING "clastext.2da" "override"
        //Find row.
        LPF find_row_index STR_VAR key = "%class%" RET row = index END

        PATCH_IF (row = 0 - 1) BEGIN
            PATCH_FAIL "set_clastext_description: class '%class%' not found in 'clastext.2da'."
        END

        //Set description.
        COUNT_2DA_COLS cols
        SET_2DA_ENTRY row 4 cols tlk
    PRETTY_PRINT_2DA
    BUT_ONLY
END

DEFINE_ACTION_FUNCTION set_kitlist_description INT_VAR tlk = 0 STR_VAR kit = "" BEGIN
    COPY_EXISTING "kitlist.2da" "override"
        //Find row.
        LPF find_row_index INT_VAR col = 1 STR_VAR key = "%kit%" RET row = index END

        PATCH_IF (row = 0 - 1) BEGIN
            PATCH_FAIL "set_kitlist_description: kit '%kit%' not found in 'kitlist.2da'."
        END

        //Set description.
        COUNT_2DA_COLS cols
        SET_2DA_ENTRY row 4 cols tlk
    PRETTY_PRINT_2DA
    BUT_ONLY
END

DEFINE_ACTION_FUNCTION set_enginest_class_description INT_VAR tlk = 0 STR_VAR class = "" BEGIN
    //Filter out blackguards; have no entry.
    OUTER_TEXT_SPRINT string "STRREF_GUI_HELP_%class%"
    ACTION_TO_UPPER string

    COPY_EXISTING "enginest.2da" "override"
        //Set description.
        COUNT_2DA_COLS cols
        COUNT_2DA_ROWS cols row_count
        found = 0
        FOR (i = 0 ; (i < row_count) AND (found != 1) ; ++i) BEGIN
            READ_2DA_ENTRY i 0 cols value
            PATCH_IF ("%string%" STRING_EQUAL_CASE "%value%") BEGIN
                SET_2DA_ENTRY i 1 cols tlk
                found = 1
            END
        END

        PATCH_IF (found = 0) BEGIN
            PATCH_FAIL "set_enginest_class_description: string '%string%' for class '%class%' not found in 'enginest.2da'."
        END
    BUT_ONLY
END

DEFINE_ACTION_FUNCTION set_enginest_kit_description INT_VAR tlk = 0 STR_VAR kit = "" BEGIN
    OUTER_TEXT_SPRINT string "STRREF_GUI_HELP_KIT_%kit%"
    ACTION_TO_UPPER string

    COPY_EXISTING "enginest.2da" "override"
        //Set description.
        COUNT_2DA_COLS cols
        COUNT_2DA_ROWS cols row_count
        found = 0
        FOR (i = 0 ; (i < row_count) AND (found != 1) ; ++i) BEGIN
            READ_2DA_ENTRY i 0 cols value
            PATCH_IF ("%string%" STRING_EQUAL_CASE "%value%") BEGIN
                SET_2DA_ENTRY i 1 cols tlk
                found = 1
            END
        END

        PATCH_IF (found = 0) BEGIN
            PATCH_FAIL "set_enginest_kit_description: string '%string%' for kit '%kit%' not found in 'enginest.2da'."
        END
    BUT_ONLY
END


//Descriptions.
DEFINE_DIMORPHIC_FUNCTION set_class_description INT_VAR descr = 0 STR_VAR class = "" BEGIN
    OUTER_SET class_descr = RESOLVE_STR_REF ((AT descr))

    LAF set_clastext_description INT_VAR tlk = class_descr STR_VAR class = "%class%" END
    LAF set_enginest_class_description INT_VAR tlk = class_descr STR_VAR class = "%class%" END
END

DEFINE_DIMORPHIC_FUNCTION set_kit_description INT_VAR descr = 0 STR_VAR kit = "" BEGIN
    OUTER_SET kit_descr = RESOLVE_STR_REF ((AT descr))

    LAF get_array_element STR_VAR
        array = "kit_classes"
        key = "%kit%"
        default = "%kit%"
    RET kit_name = value END

    LAF set_clastext_description INT_VAR tlk = kit_descr STR_VAR class = "%kit_name%" END
    LAF set_kitlist_description INT_VAR tlk = kit_descr STR_VAR kit = "%kit%" END

    //Gui editing.
    LAF get_array_element STR_VAR
        array = "kit_names"
        key = "%kit%"
        default = "%kit%"
    RET kit_name = value END
    ACTION_TO_UPPER kit_name

    ACTION_IF NOT ("%kit_name%" STRING_EQUAL_CASE "*") BEGIN
        LAF set_enginest_kit_description INT_VAR tlk = kit_descr STR_VAR kit = "%kit_name%" END
    END
END


//Proficiencies.
DEFINE_PATCH_FUNCTION get_proficiency STR_VAR kit = "" proficiency = "" RET level BEGIN
    level = 0 - 1
    //Find kit.
    LPF find_column_index STR_VAR key = "%kit%" RET col = index END
    PATCH_IF NOT (col = 0 - 1) BEGIN
        //Find proficiency: from the end to skip BG1 proficiencies with overlapping names.
        LPF find_row_index_from_end STR_VAR key = "%proficiency%" RET row = index END
        PATCH_IF NOT (row = 0 - 1) BEGIN
            COUNT_2DA_COLS cols
            READ_2DA_ENTRY row col cols level
        END
    END
END

DEFINE_PATCH_FUNCTION set_proficiency INT_VAR level = 0 STR_VAR kit = "" proficiency = "" BEGIN
    //Sanitize level.
    PATCH_IF NOT ((0 <= level) AND (level <= 5)) BEGIN
        PATCH_FAIL "set_proficiency: '%level%' must be in the [0-5] range."
    END

    //Find kit.
    LPF find_column_index STR_VAR key = "%kit%" RET col = index END
    PATCH_IF NOT (col = 0 - 1) BEGIN
        LPF find_row_index_from_end STR_VAR key = "%proficiency%" RET row = index END
        PATCH_IF NOT (row = 0 - 1) BEGIN
            COUNT_2DA_COLS cols
            SET_2DA_ENTRY row col cols level
        END ELSE BEGIN
            PATCH_FAIL "set_proficiency: proficiency '%proficiency%' not found in 'weapprof.2da'."
        END
    END ELSE BEGIN
        PATCH_FAIL "set_proficiency: kit '%kit%' not found in 'weapprof.2da'."
    END
END

//Batch patchers.
DEFINE_PATCH_FUNCTION patch_proficiencies STR_VAR kit = "" array = "" BEGIN
    PHP_EACH "%array%" AS prof => level BEGIN
        LPF set_proficiency INT_VAR
            level = level
        STR_VAR
            kit = "%kit%"
            proficiency = "%prof%"
        END
    END
END

//Clab patchers.
DEFINE_PATCH_FUNCTION make_clab_replacements STR_VAR table = "" BEGIN
    COUNT_2DA_COLS cols
    COUNT_2DA_ROWS cols rows

    FOR (i = 0 ; i < rows ; ++i) BEGIN
        FOR (j = 1 ; j < cols ; ++j) BEGIN
            READ_2DA_ENTRY i j cols value

            //Guarantee it is not a string of *'s.
            PATCH_IF NOT (("%value%" STRING_MATCHES_REGEXP "^\**$") = 0) BEGIN
                //Deconstruct into prefix + ability.
                LPF deconstruct_ability STR_VAR
                    ability = "%value%"
                RET prefix resource END

                //Sanitize.
                PATCH_IF NOT (("%prefix%" STRING_EQUAL_CASE "AP") OR ("%prefix%" STRING_EQUAL_CASE "GA")) BEGIN
                    PATCH_FAIL "make_clab_replacements: malformed ability '%value%'."
                END

                TEXT_SPRINT ability $"%table%"("%resource%")
                PATCH_IF NOT (FILE_EXISTS_IN_GAME "%ability%.spl") BEGIN
                    PATCH_FAIL "make_clab_replacements: '%resource%' unknown or does not exist in game."
                END

                TEXT_SPRINT prefixed_ability "%prefix%_%ability%"
                TO_UPPER prefixed_ability
                SET_2DA_ENTRY i j cols "%prefixed_ability%"
            END
        END
    END
END

//Auxiliary functions used by add_new_spells_clab.
DEFINE_DIMORPHIC_FUNCTION get_spell_progression_array STR_VAR kit = "" RET_ARRAY progression_array BEGIN
    LAF get_class_spell_progression STR_VAR kit = "%kit%" RET table END
    ACTION_IF ("%kit%" STRING_EQUAL_CASE "*") BEGIN
        FAIL "get_spell_progression_array: unknown '%kit%'."
    END

    ACTION_DEFINE_ASSOCIATIVE_ARRAY progression_array BEGIN END
    COPY_EXISTING "%table%.2da" "override"
        COUNT_2DA_COLS columns
        FOR (i = 1; i < columns; ++i) BEGIN
            LPF find_row_index INT_VAR col = i STR_VAR key = "1" RET char_level = index END
            TEXT_SPRINT $"progression_array"("%i%") "%char_level%"
        END
    BUT_ONLY
END

DEFINE_DIMORPHIC_FUNCTION get_spell_ability_array STR_VAR kit = "" table = "" RET_ARRAY ability_array BEGIN
    //Sanitize.
    ACTION_IF NOT (FILE_EXISTS "%table%") BEGIN
        FAIL "get_spell_ability_array: table '%table%' does not exist."
    END

    //Get spell progression array.
    ACTION_DEFINE_ASSOCIATIVE_ARRAY ability_array BEGIN END
    LAF get_spell_progression_array STR_VAR kit = "%kit%" RET_ARRAY progression_array END
    COPY - "%table%" "override"
        COUNT_2DA_COLS cols
        COUNT_2DA_ROWS cols rows
        
        //Sanitize.
        LPF get_array_count STR_VAR array = "progression_array" RET col_count = count END
        PATCH_IF cols > col_count BEGIN
            PATCH_FAIL "get_spell_ability_array: expected <= '%col_count%' columns in '%table%', got '%cols%'."
        END

        FOR (i = 0; i < rows; ++i) BEGIN
            FOR (j = 1; j < cols; ++j) BEGIN
                TEXT_SPRINT char_level $"progression_array"("%j%")
                READ_2DA_ENTRY i j cols ability
                TEXT_SPRINT $"ability_array"("%i%") "%char_level%_%ability%"
            END
        END
END

DEFINE_DIMORPHIC_FUNCTION deconstruct_level_ability STR_VAR string = "" RET level ability BEGIN
    OUTER_TEXT_SPRINT regexp "^\([1-9]\)_\(.*\)$"
    //Extract values via regexp matching.
    OUTER_INNER_PATCH "%string%" BEGIN
        REPLACE_EVALUATE "%regexp%" BEGIN
            PATCH_IF ("%string%" STRING_EQUAL_CASE "%MATCH0%") BEGIN
                TEXT_SPRINT level "%MATCH1%"
                TEXT_SPRINT ability "%MATCH2%"
            END ELSE BEGIN
                PATCH_FAIL "deconstruct_level_ability: '%string%' is not a valid levelled spell ability."
            END
        END ~~
    END
END

DEFINE_PATCH_FUNCTION add_new_spells_clab STR_VAR kit = "" table = "" BEGIN
END
