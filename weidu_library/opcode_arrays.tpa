//Libraries.
INCLUDE "%WEIDU_LIBRARY_DIR%/arrays.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/encoders.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/opcodes.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/references.tpa"


//Arrays.
LAF load_array STR_VAR
    table = "%WEIDU_LIBRARY_DIR%/resources/2da/opcode_arrays/readers.2da"
RET_ARRAY opcode_field_read_encoders = array END

LAF load_array STR_VAR
    table = "%WEIDU_LIBRARY_DIR%/resources/2da/opcode_arrays/writers.2da"
RET_ARRAY opcode_field_write_encoders = array END


//Encoders.
DEFINE_DIMORPHIC_FUNCTION encode_opcode_parameter STR_VAR value = "" RET return BEGIN
    ACTION_IF NOT (IS_AN_INT value) BEGIN
        FAIL "encode_opcode_parameter: value '%value%' is not an integer."
    END

    OUTER_TEXT_SPRINT return "#num:%value%"
END

DEFINE_DIMORPHIC_FUNCTION encode_opcode_power STR_VAR value = "" RET return BEGIN
    ACTION_IF NOT (IS_AN_INT value) BEGIN
        FAIL "encode_opcode_power: value '%value%' is not an integer."
    END

    ACTION_IF NOT ((0 <= value) AND (value <= 9)) BEGIN
        FAIL "encode_opcode_power: value '%value%' is not in the [0-9] range."
    END

    OUTER_TEXT_SPRINT return "%value%"
END

DEFINE_DIMORPHIC_FUNCTION encode_opcode_probability STR_VAR value = "" RET return BEGIN
    ACTION_IF NOT (IS_AN_INT value) BEGIN
        FAIL "encode_opcode_probability: value '%value%' is not an integer."
    END

    ACTION_IF NOT ((0 <= value) AND (value <= 100)) BEGIN
        FAIL "encode_opcode_probability: value '%value%' is not in the [0-100] range."
    END

    OUTER_TEXT_SPRINT return "%value%"
END

DEFINE_DIMORPHIC_FUNCTION encode_opcode_resource STR_VAR value = "" RET return BEGIN
    OUTER_TEXT_SPRINT return "#nul:"
    ACTION_IF NOT ("%value%" STRING_EQUAL_CASE "") BEGIN
        OUTER_TEXT_SPRINT return "#lit:%value%"
    END
END


//Opcodes as arrays.
DEFINE_PATCH_FUNCTION get_opcode_array INT_VAR offset = 0 RET_ARRAY array BEGIN
    DEFINE_ASSOCIATIVE_ARRAY array BEGIN END

    PHP_EACH "opc_offsets" AS field => _ BEGIN
        LPF get_opcode_field INT_VAR
            offset = offset
        STR_VAR
            field = "%field%"
        RET value END

        //Convert value.
        TEXT_SPRINT encoder $"opcode_field_read_encoders"("%field%")
        LPF "%encoder%" STR_VAR value = "%value%" RET value = return END
        TEXT_SPRINT $"array"("%field%") "%value%"
    END
END

DEFINE_PATCH_FUNCTION set_opcode_array INT_VAR offset = 0 STR_VAR array = "" BEGIN
    PHP_EACH "%array%" AS field => value BEGIN
        TEXT_SPRINT encoder $"opcode_field_write_encoders"("%field%")
        LPF "%encoder%" STR_VAR value = "%value%" RET value = return END
        PATCH_IF NOT ("%value%" STRING_EQUAL_CASE "*") BEGIN
            LPF set_opcode_field INT_VAR
                offset = offset
            STR_VAR
                field = "%field%"
                value = "%value%"
            END
        END
    END
END


//Creating values.
DEFINE_DIMORPHIC_FUNCTION get_zero_opcode RET_ARRAY array BEGIN
    ACTION_DEFINE_ASSOCIATIVE_ARRAY array BEGIN
        opcode          => "ac_damage_type"
        target          => "projectile"
        power           => "0"
        parameter1      => "#num:0"
        parameter2      => "#num:0"
        timing          => "limited"
        resist_dispel   => "natural"
        duration        => "0"
        probability1    => "100"
        probability2    => "0"
        resource        => "#nul:"
        dicenumber      => "0"
        dicesize        => "0"
        savingthrow     => "#num:0"
        savebonus       => "0"
        special         => "#num:0"
    END
END

DEFINE_DIMORPHIC_FUNCTION get_equipped_zero_opcode RET_ARRAY array BEGIN
    LAF get_zero_opcode RET_ARRAY array END

    //Equipped fields.
    OUTER_TEXT_SPRINT $"array"("target") "self"
    OUTER_TEXT_SPRINT $"array"("timing") "equipped"
END
