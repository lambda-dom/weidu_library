//Libraries.
INCLUDE "%WEIDU_LIBRARY_DIR%/arrays.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/encoders.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/opcodes.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/references.tpa"


//Arrays.
ACTION_DEFINE_ASSOCIATIVE_ARRAY opcode_field_encoders BEGIN
    "parameter1"    => "encode_opcode_parameter"
    "parameter2"    => "encode_opcode_parameter"
    "power"         => "encode_opcode_power"
    "duration"      => "encode_positive"
    "probability1"  => "encode_opcode_probability"
    "probability2"  => "encode_opcode_probability"
    "dicesize"      => "encode_positive"
    "dicenumber"    => "encode_positive"
    "savingthrow"   => "encode_opcode_parameter"
    "savebonus"     => "encode_opcode_save_bonus"
    "special"       => "encode_opcode_parameter"
END


//Encoders.
DEFINE_DIMORPHIC_FUNCTION encode_opcode_parameter STR_VAR value = "" RET return BEGIN
    ACTION_IF NOT (IS_AN_INT value) BEGIN
        FAIL "encode_opcode_parameter: value '%value%' is not an integer."
    END

    OUTER_TEXT_SPRINT return "#num:%value%"
END

DEFINE_DIMORPHIC_FUNCTION encode_opcode_power STR_VAR value = "" RET return BEGIN
    ACTION_IF NOT (IS_AN_INT value) BEGIN
        FAIL "encode_opcode_power: value '%value%' is not an integer."
    END

    ACTION_IF NOT ((0 <= value) AND (value <= 9)) BEGIN
        FAIL "encode_opcode_power: value '%value%' is not in the [0-9] range."
    END

    OUTER_TEXT_SPRINT return "%value%"
END

DEFINE_DIMORPHIC_FUNCTION encode_opcode_probability STR_VAR value = "" RET return BEGIN
    ACTION_IF NOT (IS_AN_INT value) BEGIN
        FAIL "encode_opcode_probability: value '%value%' is not an integer."
    END

    ACTION_IF NOT ((0 <= value) AND (value <= 100)) BEGIN
        FAIL "encode_opcode_probability: value '%value%' is not in the [0-100] range."
    END

    OUTER_TEXT_SPRINT return "%value%"
END

DEFINE_DIMORPHIC_FUNCTION encode_opcode_resource STR_VAR value = "" RET return BEGIN
    OUTER_TEXT_SPRINT return "#nul:"
    ACTION_IF NOT ("%value%" STRING_EQUAL_CASE "") BEGIN
        OUTER_TEXT_SPRINT return "#lit:%value%"
    END
END

//Opcodes as arrays.
DEFINE_PATCH_FUNCTION get_opcode_array INT_VAR offset = 0 RET_ARRAY array BEGIN
    DEFINE_ASSOCIATIVE_ARRAY array BEGIN END

    //Opcode type.
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "opcode" RET value END
    LPF find_key STR_VAR
        array = "opcode_types"
        value = "%value%"
    RET opcode_id = key END
    PATCH_IF ("%opcode_id%" STRING_EQUAL_CASE "*") BEGIN
        PATCH_FAIL "get_opcode_array: opcode value '%value%' at offset '%offset%' unknown."
    END
    TEXT_SPRINT $array("opcode") "%opcode_id%"

    //Opcode target.
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "target" RET value END
    LPF find_key STR_VAR
        array = "opcode_targets"
        value = "%value%"
    RET target_id = key END
    PATCH_IF ("%target_id%" STRING_EQUAL_CASE "*") BEGIN
        PATCH_FAIL "get_opcode_array: opcode target '%value%' at offset '%offset%' unknown."
    END
    TEXT_SPRINT $array("target") "%target_id%"

    //Opcode power.
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "power" RET value END
    TEXT_SPRINT $array("power") "%value%"

    //Opcode parameters.
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "parameter1" RET value END
    TEXT_SPRINT $array("parameter1") "#num:%value%"
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "parameter2" RET value END
    TEXT_SPRINT $array("parameter2") "#num:%value%"

    //Opcode timing.
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "timing" RET value END
    LPF find_key STR_VAR
        array = "opcode_timings"
        value = "%value%"
    RET timing_id = key END
    PATCH_IF ("%timing_id%" STRING_EQUAL_CASE "*") BEGIN
        PATCH_FAIL "get_opcode_array: opcode timing '%value%' at offset '%offset%' unknown."
    END
    TEXT_SPRINT $array("timing") "%timing_id%"

    //Opcode resist_dispel.
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "resist_dispel" RET value END
    LPF find_key STR_VAR
        array = "opcode_resist_dispels"
        value = "%value%"
    RET resist_dispel_id = key END
    PATCH_IF ("%resist_dispel_id%" STRING_EQUAL_CASE "*") BEGIN
        PATCH_FAIL "get_opcode_array: opcode resist_dispel '%value%' at offset '%offset%' unknown."
    END
    TEXT_SPRINT $array("resist_dispel") "%resist_dispel_id%"

    //Opcode duration.
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "duration" RET value END
    TEXT_SPRINT $array("duration") "%value%"

    //Opcode probability.
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "probability1" RET value END
    TEXT_SPRINT $array("probability1") "%value%"
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "probability2" RET value END
    TEXT_SPRINT $array("probability2") "%value%"

    //Opcode resource.
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "resource" RET value END
    PATCH_IF ("%value%" STRING_EQUAL_CASE "") BEGIN
        TEXT_SPRINT $array("resource") "#nul:"
    END ELSE BEGIN
        TEXT_SPRINT $array("resource") "#lit:%value%"
    END

    //Opcode dice.
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "dicesize" RET value END
    TEXT_SPRINT $array("dicesize") "%value%"
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "dicenumber" RET value END
    TEXT_SPRINT $array("dicenumber") "%value%"

    //Opcode saves.
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "savingthrow" RET value END
    TEXT_SPRINT $array("savingthrow") "#num:%value%"
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "savebonus" RET value END
    TEXT_SPRINT $array("savebonus") "%value%"

    //Opcode special.
    LPF get_opcode_field INT_VAR offset = offset STR_VAR field = "special" RET value END
    TEXT_SPRINT $array("special") "#num:%value%"
END

DEFINE_PATCH_FUNCTION set_opcode_array INT_VAR offset = 0 STR_VAR array = "" BEGIN
    TEXT_SPRINT value $"%array%"("opcode")
    LPF get_opcode_type_id STR_VAR value = "%value%" RET opcode_id = id END
    LPF set_opcode_field INT_VAR offset = offset STR_VAR field = "opcode" value = "%opcode_id%" END

    TEXT_SPRINT value $"%array%"("target")
    LPF get_opcode_target_id STR_VAR value = "%value%" RET target_id = id END
    LPF set_opcode_field INT_VAR offset = offset STR_VAR field = "target" value = "%target_id%" END

    TEXT_SPRINT value $"%array%"("power")
    LPF set_opcode_field INT_VAR offset = offset STR_VAR field = "power" value = "%value%" END

    TEXT_SPRINT value $"%array%"("parameter1")
    LPF encode_table_reference STR_VAR value = "%value%" RET value = return END
    LPF set_opcode_field INT_VAR offset = offset STR_VAR field = "parameter1" value = "%value%" END
    TEXT_SPRINT value $"%array%"("parameter2")
    LPF encode_table_reference STR_VAR value = "%value%" RET value = return END
    LPF set_opcode_field INT_VAR offset = offset STR_VAR field = "parameter2" value = "%value%" END

    TEXT_SPRINT value $"%array%"("duration")
    LPF set_opcode_field INT_VAR offset = offset STR_VAR field = "duration" value = "%value%" END

    //Probability.
    TEXT_SPRINT value $"%array%"("probability1")
    LPF set_opcode_field INT_VAR offset = offset STR_VAR field = "probability1" value = "%value%" END
    TEXT_SPRINT value $"%array%"("probability2")
    LPF set_opcode_field INT_VAR offset = offset STR_VAR field = "probability2" value = "%value%" END

    //Resource.
    TEXT_SPRINT value $"%array%"("resource")
    LPF encode_table_reference STR_VAR value = "%value%" RET value = return END
    LPF set_opcode_field INT_VAR offset = offset STR_VAR field = "resource" value = "%value%" END

    //Dice.
    TEXT_SPRINT value $"%array%"("dicenumber")
    LPF set_opcode_field INT_VAR offset = offset STR_VAR field = "dicenumber" value = "%value%" END
    TEXT_SPRINT value $"%array%"("dicesize")
    LPF set_opcode_field INT_VAR offset = offset STR_VAR field = "dicesize" value = "%value%" END

    //Savingthrow.
    TEXT_SPRINT value $"%array%"("savingthrow")
    LPF encode_table_reference STR_VAR value = "%value%" RET value = return END
    LPF set_opcode_field INT_VAR offset = offset STR_VAR field = "savingthrow" value = "%value%" END

    TEXT_SPRINT value $"%array%"("savebonus")
    LPF set_opcode_field INT_VAR offset = offset STR_VAR field = "savebonus" value = "%value%" END

    //Special.
    TEXT_SPRINT value $"%array%"("special")
    LPF encode_table_reference STR_VAR value = "%value%" RET value = return END
    LPF set_opcode_field INT_VAR offset = offset STR_VAR field = "special" value = "%value%" END
END


//Creating values.
DEFINE_DIMORPHIC_FUNCTION get_zero_opcode RET_ARRAY array BEGIN
    ACTION_DEFINE_ASSOCIATIVE_ARRAY array BEGIN
        opcode          => "ac_damage_type"
        target          => "projectile"
        power           => "0"
        parameter1      => "#num:0"
        parameter2      => "#num:0"
        timing          => "limited"
        resist_dispel   => "natural"
        duration        => "0"
        probability1    => "100"
        probability2    => "0"
        resource        => "#nul:"
        dicenumber      => "0"
        dicesize        => "0"
        savingthrow     => "#num:0"
        savebonus       => "0"
        special         => "#num:0"
    END
END

DEFINE_DIMORPHIC_FUNCTION get_equipped_zero_opcode RET_ARRAY array BEGIN
    LAF get_zero_opcode RET_ARRAY array END

    //Equipped fields.
    OUTER_TEXT_SPRINT $"array"("target") "self"
    OUTER_TEXT_SPRINT $"array"("timing") "equipped"
END
