//Libraries.
INCLUDE "%WEIDU_LIBRARY_DIR%/arrays.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/effects.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/encoders.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/installers.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/references.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/sectypes.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/tables.tpa"


//Arrays.
LAF load_array STR_VAR
    table = "%WEIDU_LIBRARY_DIR%/resources/2da/installers/effects/encoders.2da"
RET_ARRAY effect_encoders = array END


//Installers.
DEFINE_ACTION_FUNCTION copy_effects STR_VAR
    table = ""
    resources_dir = ""
    patches = "*"
BEGIN
    //Sanitize.
    ACTION_IF NOT ("%patches%" STRING_EQUAL_CASE "*") BEGIN
        ACTION_IF NOT (FILE_EXISTS "%patches%") BEGIN
            FAIL "copy_effects: patches file '%patches%' does not exist."
        END

        //Include patches file.
        INCLUDE "%patches%"
    END

    ACTION_IF NOT (DIRECTORY_EXISTS "%resources_dir%") BEGIN
        FAIL "copy_effects: resources directory '%resources_dir%' does not exist."
    END

    //Load table
    LAF load_table STR_VAR
        file = "%table%"
        fields = "resource,install,patch,override,destination"
    RET_ARRAY resources = table END

    //Loop.
    LAF get_table_row_count STR_VAR table = "resources" RET row_count = count END
    OUTER_FOR (i = 0; i < row_count; ++i) BEGIN
        LAF get_row_from_table INT_VAR row = i STR_VAR
            table = "resources"
        RET_ARRAY
            row = array
        END

        WITH_SCOPE BEGIN
            //Inject names in local scope.
            OUTER_TEXT_SPRINT resource $"row"("resource")
            OUTER_TEXT_SPRINT install $"row"("install")
            OUTER_TEXT_SPRINT patch $"row"("patch")
            OUTER_TEXT_SPRINT override $"row"("override")
            OUTER_TEXT_SPRINT destination $"row"("destination")

            ACTION_IF NOT (install = 0) BEGIN
                //Compute destination.
                LAF encode_table_reference STR_VAR
                    value = "%destination%"
                RET destination = return END

                //Sanitize.
                LAF sanitize_resource STR_VAR
                    override = "%override%"
                    destination = "%destination%.eff"
                    resource = "%resource%"
                END

                COPY "%resources_dir%/%resource%.eff" "override/%destination%.eff"
                    PATCH_IF NOT ("%patch%" STRING_EQUAL_CASE "*") BEGIN
                        LPF "%patch%" STR_VAR destination = "%destination%" END
                    END
            END
        END
    END
END

DEFINE_ACTION_FUNCTION create_effects STR_VAR table = "" tra = "*" patches = "*" BEGIN
    //Sanitize.
    ACTION_IF NOT ("%patches%" STRING_EQUAL_CASE "*") BEGIN
        ACTION_IF NOT (FILE_EXISTS "%patches%") BEGIN
            FAIL "create_effects: patches file '%patches%' does not exist."
        END

        //Include patches file.
        INCLUDE "%patches%"
    END

    //Normalize tra.
    ACTION_IF ("%tra%" STRING_EQUAL_CASE "*") BEGIN
        OUTER_TEXT_SPRINT tra "%WEIDU_LIBRARY_DIR%/resources/tra/empty.tra"
    END

    //Existence checks.
    ACTION_IF NOT (FILE_EXISTS "%tra%") BEGIN
        FAIL "create_effects: tra file '%tra%' does not exist."
    END

    //Load table
    LAF load_table STR_VAR
        file = "%table%"
        fields = "resource,install,opcode,target,parameter1,parameter2,parameter3,parameter4,power,timing,duration,probability1,probability2,dicenumber,dicesize,savingthrow,savebonus,resist_dispel,special,resource,resource2,resource3,projectile,school,sectype,patch,override,destination"
    RET_ARRAY resources = table END

    WITH_TRA "%tra%" BEGIN
        //Loop.
        LAF get_table_row_count STR_VAR table = "resources" RET row_count = count END
        OUTER_FOR (i = 0; i < row_count; ++i) BEGIN
            LAF get_row_from_table INT_VAR row = i STR_VAR
                table = "resources"
            RET_ARRAY
                row = array
            END

            LAF split_array_with_fields STR_VAR
                fields = "resource,install,patch,override,destination"
                array = "row"
            RET_ARRAY split remainder END

            WITH_SCOPE BEGIN
                //Inject names in local scope.
                OUTER_TEXT_SPRINT resource $"split"("resource")
                OUTER_TEXT_SPRINT install $"split"("install")
                OUTER_TEXT_SPRINT patch $"split"("patch")
                OUTER_TEXT_SPRINT override $"split"("override")
                OUTER_TEXT_SPRINT destination $"split"("destination")

                ACTION_IF NOT (install = 0) BEGIN
                    //Compute destination.
                    LAF encode_table_reference STR_VAR
                        value = "%destination%"
                    RET destination = return END

                    //Sanitize.
                    LAF sanitize_resource STR_VAR
                        override = "%override%"
                        destination = "%destination%.eff"
                        resource = "%resource%"
                    END

                    CREATE EFF "%destination%"
                        //Write defaults.
                        LPF set_effect_field STR_VAR
                            field = "probability1"
                            value = "100"
                        END

                        PHP_EACH "remainder" AS field => value BEGIN
                            PATCH_IF NOT ("%value%" STRING_EQUAL_CASE "*") BEGIN
                                TEXT_SPRINT encoder $"effect_encoders"("%field%")
                            END

                            LPF "%encoder%" STR_VAR value = "%value%" RET return END
                            PATCH_IF NOT ("%return%" STRING_EQUAL_CASE "*") BEGIN
                                LPF set_effect_field STR_VAR
                                    field = "%field%"
                                    value = "%return%"
                                END
                            END
                        END

                        PATCH_IF NOT ("%patch%" STRING_EQUAL_CASE "*") BEGIN
                            LPF "%patch%" STR_VAR destination = "%destination%" END
                        END
                END
            END
        END
    END
END
