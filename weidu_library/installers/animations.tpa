//Libraries.
INCLUDE "%WEIDU_LIBRARY_DIR%/installers.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/references.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/tables.tpa"



//Installers.
DEFINE_ACTION_FUNCTION install_animations STR_VAR table = "" resources_dir = "" patches = "*" BEGIN
    //Sanitize.
    ACTION_IF NOT ("%patches%" STRING_EQUAL_CASE "*") BEGIN
        ACTION_IF NOT (FILE_EXISTS "%patches%") BEGIN
            FAIL "install_animations: patches file '%patches%' does not exist."
        END

        //Include patches file.
        INCLUDE "%patches%"
    END

    //Load table
    LAF load_table STR_VAR
        file = "%table%"
        fields = "resource,install,animation,patch,override,destination"
    RET_ARRAY resources = table END

    //Loop.
    LAF get_table_row_count STR_VAR table = "resources" RET row_count = count END
    OUTER_FOR (i = 0; i < row_count; ++i) BEGIN
        LAF get_row_from_table INT_VAR row = i STR_VAR
            table = "resources"
        RET_ARRAY
            row = array
        END

        LAF split_array_with_fields STR_VAR
            fields = "resource,install,patch,override,destination"
            array = "row"
        RET_ARRAY split remainder END

        WITH_SCOPE BEGIN
            //Inject names in local scope.
            OUTER_TEXT_SPRINT resource $"split"("resource")
            OUTER_TEXT_SPRINT install $"split"("install")
            OUTER_TEXT_SPRINT patch $"split"("patch")
            OUTER_TEXT_SPRINT override $"split"("override")
            OUTER_TEXT_SPRINT destination $"split"("destination")

            ACTION_IF NOT (install = 0) BEGIN
                //Compute destination.
                ACTION_IF NOT ("%destination%" STRING_EQUAL_CASE "*") BEGIN
                    LAF encode_table_reference STR_VAR
                        value = "%destination%"
                    RET destination = return END
                END

                //Sanitize.
                LAF sanitize_resource STR_VAR
                    override = "%override%"
                    destination = "%destination%.vvc"
                    resource = "%resource%"
                END

                COPY "%resources_dir%/%resource%.vvc" "override/%destination%.vvc"
                    PATCH_WITH_SCOPE BEGIN
                        //Inject names in local scope.
                        TEXT_SPRINT animation $"remainder"("animation")

                        LPF encode_table_reference STR_VAR
                            value = "%animation%"
                        RET animation_res = return END

                        PATCH_IF NOT (FILE_EXISTS_IN_GAME "%animation_res%.bam") BEGIN
                            PATCH_FAIL "install_animations: '%animation_res%.bam' for vvc '%resource%' does not exist in game."
                        END
                        
                        LPF set_vvc_field STR_VAR
                            field = "animation"
                            value = "%animation_res%"
                        END
                    END

                    PATCH_IF NOT ("%patch%" STRING_EQUAL_CASE "*") BEGIN
                        LPF "%patch%" STR_VAR destination = "%destination%" END
                    END
            END
        END
    END
END
