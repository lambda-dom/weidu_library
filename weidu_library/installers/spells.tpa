//Libraries.
INCLUDE "%WEIDU_LIBRARY_DIR%/arrays.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/installers.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/references.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/sectypes.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/spells.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/tables.tpa"


//Spell encoders.
LAF load_array STR_VAR
    table = "%WEIDU_LIBRARY_DIR%/resources/2da/installers/spells/encoders.2da"
RET_ARRAY spell_encoders = array END

//Tables and arrays.
LAF load_table STR_VAR
    file = "%WEIDU_LIBRARY_DIR%/resources/2da/spells/school_defaults.2da"
    fields = "school,animation,priest,wizard,opposite"
RET_ARRAY defaults = table END

LAF array_from_table STR_VAR
    table = "defaults"
    key = "school"
    value = "animation"
RET_ARRAY animations = array END

LAF array_from_table STR_VAR
    table = "defaults"
    key = "school"
    value = "priest"
RET_ARRAY sounds_priest = array END

LAF array_from_table STR_VAR
    table = "defaults"
    key = "school"
    value = "priest"
RET_ARRAY sounds_wizard = array END

LAF array_from_table STR_VAR
    table = "defaults"
    key = "school"
    value = "opposite"
RET_ARRAY opposites = array END


//Encoders.
DEFINE_PATCH_FUNCTION encode_ability_icon STR_VAR value = "" RET return BEGIN
    //Compute reference.
    LPF encode_bam_ref STR_VAR
        value = "%value%"
    RET return END

    LPF set_spell_header_fields STR_VAR
        field = "icon"
        value = "%return%"
    END

    //Return nothing.
    TEXT_SPRINT return "*"
END

DEFINE_PATCH_FUNCTION encode_projectile_header STR_VAR value = "" RET return BEGIN
    //Get projectile.
    LPF encode_projectile_ref STR_VAR
        value = "%value%"
    RET id = return END

    LPF set_spell_header_fields STR_VAR
        field = "projectile"
        value = "%id%"
    END

    //Return nothing.
    TEXT_SPRINT return "*"
END

DEFINE_PATCH_FUNCTION encode_spell_range STR_VAR value = "" RET return BEGIN
    PATCH_IF NOT (IS_AN_INT value) BEGIN
        PATCH_FAIL "encode_spell_range: value '%value%' is not an integer."
        PATCH_IF NOT (value >= 0) BEGIN
            PATCH_FAIL "encode_spell_range: value '%value%' is not positive."
        END
    END

    LPF set_spell_header_fields STR_VAR
        field = "range"
        value = "%value%"
    END

    //Return nothing.
    TEXT_SPRINT return "*"
END

DEFINE_PATCH_FUNCTION encode_spell_speed STR_VAR value = "" RET return BEGIN
    PATCH_IF NOT (IS_AN_INT value) BEGIN
        PATCH_FAIL "encode_spell_speed: value '%value%' is not an integer."
        PATCH_IF NOT ((0 <= value) AND (value <= 9)) BEGIN
            PATCH_FAIL "encode_spell_speed: value '%value%' not in the [0-9] range."
        END
    END

    LPF set_spell_header_fields STR_VAR
        field = "speed"
        value = "%value%"
    END

    //Return nothing.
    TEXT_SPRINT return "*"
END

DEFINE_PATCH_FUNCTION encode_spell_flags_mask STR_VAR value = "" RET return BEGIN
    LPF encode_spell_flags STR_VAR value = "%value%" RET lhand = return END
    LPF set_spell_field STR_VAR field = "flags" value = "%value%" END
    TEXT_SPRINT return "*"
END


//Auxiliary patchers.
DEFINE_PATCH_FUNCTION write_spell_defaults STR_VAR fields = "" BEGIN
    //Extract arguments.
    TEXT_SPRINT school $"%fields%"("school")
    TEXT_SPRINT type $"%fields%"("type")

    PATCH_IF NOT ("%school%" STRING_EQUAL_CASE "*") BEGIN
        //Normalize.
        TO_LOWER school
        TO_LOWER type

        //Set animation if known school.
        TEXT_SPRINT animation $"animations"("%school%")
        PATCH_IF (IS_AN_INT animation) BEGIN
            PATCH_IF NOT (0 - 1 = animation) BEGIN
                LPF set_spell_field STR_VAR
                    field = "animation"
                    value = "%animation%"
                END
            END
        END

        //Set sound if known school and type priest or wizard.
        PATCH_IF ("%type%" STRING_EQUAL_CASE "priest") OR ("%type%" STRING_EQUAL_CASE "wizard") BEGIN
            TEXT_SPRINT sound $"sounds_%type%"("%school%")
            PATCH_IF (NOT ("%sound%" STRING_EQUAL_CASE "*"))  AND (FILE_EXISTS_IN_GAME "%sound%.wav") BEGIN
                LPF set_spell_field STR_VAR
                    field = "sound"
                    value = "%sound%"
                END
            END
        END

        //Set opposition school if known school and type wizard.
        PATCH_IF ("%type%" STRING_EQUAL_CASE "wizard") BEGIN
            TEXT_SPRINT bit_index $"opposites"("%school%")
            PATCH_IF (IS_AN_INT bit_index) BEGIN
                PATCH_IF NOT (0 - 1 = bit_index) BEGIN
                    LPF get_spell_field STR_VAR
                        field = "exclusion"
                    RET exclusion = value END

                    exclude_flags = exclusion BOR (1 << bit_index)
                    LPF set_spell_field STR_VAR
                        field = "exclusion"
                        value = "%exclude_flags%"
                    END
                END
            END
        END
    END
END


//Installer.
DEFINE_ACTION_FUNCTION install_spells STR_VAR
    table = ""
    dir = ""
    tra = "*"
    patches = "*"
BEGIN
    //Sanitize.
    ACTION_IF NOT ("%patches%" STRING_EQUAL_CASE "*") BEGIN
        ACTION_IF NOT (FILE_EXISTS "%patches%") BEGIN
            FAIL "install_spells: patches file '%patches%' does not exist."
        END

        //Include patches file.
        INCLUDE "%patches%"
    END

    //Normalize tra.
    ACTION_IF ("%tra%" STRING_EQUAL_CASE "*") BEGIN
        OUTER_TEXT_SPRINT tra "%WEIDU_LIBRARY_DIR%/resources/tra/empty.tra"
    END

    //Existence checks.
    ACTION_IF NOT (FILE_EXISTS "%tra%") BEGIN
        FAIL "install_spells: tra file '%tra%' does not exist."
    END

    ACTION_IF NOT (DIRECTORY_EXISTS "%dir%") BEGIN
        FAIL "install_spells: resources directory '%dir%' does not exist."
    END

    //Load table
    LAF load_table STR_VAR
        file = "%table%"
        fields = "resource,install,name,description,type,level,flags,school,sectype,book_icon,ability_icon,projectile,range,speed,patch,override,destination"
    RET_ARRAY resources = table END

    WITH_TRA "%tra%" BEGIN
        //Loop.
        LAF get_table_row_count STR_VAR table = "resources" RET row_count = count END
        OUTER_FOR (i = 0; i < row_count; ++i) BEGIN
            LAF get_row_from_table INT_VAR row = i STR_VAR
                table = "resources"
            RET_ARRAY
                row = array
            END

            LAF split_array_with_fields STR_VAR
                fields = "resource,install,patch,override,destination"
                array = "row"
            RET_ARRAY split remainder END

            WITH_SCOPE BEGIN
                //Inject names in local scope.
                OUTER_TEXT_SPRINT resource $"split"("resource")
                OUTER_TEXT_SPRINT install $"split"("install")
                OUTER_TEXT_SPRINT patch $"split"("patch")
                OUTER_TEXT_SPRINT override $"split"("override")
                OUTER_TEXT_SPRINT destination $"split"("destination")

                ACTION_IF NOT (install = 0) BEGIN
                    //Compute destination.
                    LAF encode_table_reference STR_VAR
                        value = "%destination%"
                    RET destination = return END

                    //Sanitize.
                    LAF sanitize_resource STR_VAR
                        override = "%override%"
                        destination = "%destination%.spl"
                        resource = "%resource%"
                    END

                    COPY "%dir%/%resource%.spl" "override/%destination%.spl"
                        //Correct self-references.
                        LPF ALTER_EFFECT INT_VAR
                            silent = 1
                        STR_VAR
                            match_resource = "self#res"
                            resource = "%destination%"
                        END

                        LPF write_spell_defaults STR_VAR fields = "remainder" END
                        PHP_EACH "remainder" AS field => value BEGIN
                            PATCH_IF NOT ("%value%" STRING_EQUAL_CASE "*") BEGIN
                                //Get encoder.
                                TEXT_SPRINT encoder $"spell_encoders"("%field%")

                                //Write value.
                                LPF "%encoder%" STR_VAR value = "%value%" RET return END
                                PATCH_IF NOT ("%return%" STRING_EQUAL_CASE "*") BEGIN
                                    LPF set_spell_field STR_VAR
                                        field = "%field%"
                                        value = "%return%"
                                    END
                                END
                            END
                        END

                        PATCH_IF NOT ("%patch%" STRING_EQUAL_CASE "*") BEGIN
                            LPF "%patch%" STR_VAR destination = "%destination%" END
                        END
                END
            END
        END
    END
END
