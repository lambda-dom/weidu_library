//Libraries.
INCLUDE "%WEIDU_LIBRARY_DIR%/arrays.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/binary.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/generic.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/tables.tpa"


//Constants.
//Tables.
LAF load_offsets_table STR_VAR
    file = "%WEIDU_LIBRARY_DIR%/resources/2da/creatures/offsets.2da"
RET_ARRAY table_cre_offsets = table END

//Indexes.
LAF array_from_table STR_VAR
    table = "table_cre_offsets"
    key = "field"
    value = "offset"
RET_ARRAY cre_offsets = array END

LAF array_from_table STR_VAR
    table = "table_cre_offsets"
    key = "field"
    value = "reader"
RET_ARRAY cre_readers = array END

LAF array_from_table STR_VAR
    table = "table_cre_offsets"
    key = "field"
    value = "writer"
RET_ARRAY cre_writers = array END


//Arrays.
LAF load_array STR_VAR
    table = "%WEIDU_LIBRARY_DIR%/resources/2da/creatures/item_slots.2da"
RET_ARRAY item_slots = array END


//Constants.
DEFINE_DIMORPHIC_FUNCTION get_creature_size RET size BEGIN
    OUTER_SET size = 0x02d3
END


//Kit reader and writer.
DEFINE_PATCH_FUNCTION read_cre_kit INT_VAR offset = 0 RET value BEGIN
    READ_SHORT offset short1
    READ_SHORT (offset + 2) short2
    value = short2 + (short1 << 16)
END

DEFINE_PATCH_FUNCTION write_cre_kit INT_VAR offset = 0 STR_VAR value = "" BEGIN
    PATCH_IF NOT (IS_AN_INT "%value%") BEGIN
        PATCH_FAIL "write_long: value '%value%' is not an integer numeric value."
    END
    PATCH_IF NOT (0 <= value) BEGIN
        PATCH_FAIL "write_long: value '%value%' must be positive."
    END

    short1 = value BAND 0xff
    short2 = (value BAND 0xff00) >> 16
    WRITE_SHORT offset short2
    WRITE_SHORT (offset + 2) short1
END

//Basic functions.
DEFINE_PATCH_FUNCTION get_cre_known_spell_count RET count BEGIN
    READ_LONG 0x02a4 count
END

DEFINE_PATCH_FUNCTION get_cre_known_spell_offset RET offset BEGIN
    READ_LONG 0x02a0 offset
END

DEFINE_PATCH_FUNCTION get_cre_memorized_spell_count RET count BEGIN
    READ_LONG 0x02ac count
END

DEFINE_PATCH_FUNCTION get_cre_memorized_spell_offset RET offset BEGIN
    READ_LONG 0x02a8 offset
END

DEFINE_PATCH_FUNCTION get_cre_item_count RET count BEGIN
    READ_LONG 0x02c0 count
END

DEFINE_PATCH_FUNCTION get_cre_effect_count RET count BEGIN
    READ_LONG 0x02c8 count
END

//Readers and writers.
DEFINE_PATCH_FUNCTION get_cre_field STR_VAR field = "" RET value BEGIN
    LPF get_field_at INT_VAR
        offset = 0
    STR_VAR
        field = "%field%"
        offsets = "cre_offsets"
        readers = "cre_readers"
    RET value END
END

DEFINE_PATCH_FUNCTION set_cre_field STR_VAR field = "" value = "" BEGIN
    LPF set_field_at INT_VAR
        offset = 0
    STR_VAR
        field = "%field%"
        offsets = "cre_offsets"
        writers = "cre_writers"
        value = "%value%"
    END
END

//Encoders.
DEFINE_DIMORPHIC_FUNCTION encode_creature_slot_id STR_VAR value = "" RET return BEGIN
    OUTER_TEXT_SPRINT slot_id $"item_slots"("%value%")

    OUTER_SET return = 0 - 1
    ACTION_IF (IS_AN_INT slot_id) BEGIN
        OUTER_SET return = slot_id
    END
END

DEFINE_DIMORPHIC_FUNCTION encode_creature_animation_id STR_VAR value = "" RET return BEGIN
    ACTION_TO_UPPER proficiency
    LAF get_id_from_ids STR_VAR ids = "animate" value = "%value%" RET return = id END
END
